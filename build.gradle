//  Gant -- A Groovy way of scripting Ant tasks.
//
//  Copyright Â© 2008-9 Russel Winder
//
//  Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in
//  compliance with the License. You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software distributed under the License is
//  distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
//  implied. See the License for the specific language governing permissions and limitations under the
//  License.
//
//  Author : Russel Winder <russel.winder@concertant.com>

ant.property ( file : 'local.build.properties' )
ant.property ( file : 'build.properties' )

groovyHome = System.getenv ( ).'GROOVY_HOME'
if ( ! groovyHome ) { groovyHome = '/usr/share/gant' }
def groovyVersionPatternString = /^groovy-(all-)?([0-9].*)\.jar/
def items = ( new File ( groovyHome + '/lib' ) ).listFiles ( [ accept : { File dir , String name -> return ( name =~ groovyVersionPatternString ).find ( ) } ] as FilenameFilter )
if ( items ) {
  groovyVersion = ( items[0].name =~ groovyVersionPatternString )[0][2]
  assert groovyVersion
}
else { throw new RuntimeException ( "Cannot determine the version of Groovy being used." ) }

//groovyVersion = ant.antProject.properties.groovy16Version

println ( "Using Groovy version " + groovyVersion + " from " + groovyHome )

type = 'jar'
group = 'org.codehaus.gant'
artifact = 'gant'

version = ant.antProject.properties.gantVersion
archivesBaseName = artifact

usePlugin ( 'groovy' )

sourceCompatibility = 5
targetCompatibility = 5

repositories {
  mavenRepo ( name : 'Local' , urls : 'file:///home/users/russel/.m2/repository' )
  mavenCentral ( )
}
  
dependencies {
  groovy ( 'org.codehaus.groovy:groovy-all:' + groovyVersion )
  compile (
             'commons-cli:commons-cli:' + ant.antProject.properties.commonsCliVersion ,
             'org.apache.ant:ant:' + ant.antProject.properties.antVersion
           )
  testCompile (
                 'junit:junit:' + ant.antProject.properties.junitVersion
                  )
  testRuntime (
                 'org.apache.ivy:ivy:' + ant.antProject.properties.ivyVersion
                 )
}

test.doFirst {
  groovyAntTaskTestVersionPropertyFile = new File ( ant.antProject.properties.groovyAntTaskTestVersionPropertyFileName )
  groovyAntTaskTestVersionPropertyFile.write ( 'groovyAntTaskTestVersion = ' + groovyVersion )
}

test.doLast {
  groovyAntTaskTestVersionPropertyFile.delete ( )
}

//  The Java plugin clean task isn't flexible as as 2009-01-09 (Gradle version 0.5.2) so to remove the extra
//  detritus we have to have use one of the hooks.  For no reason choose doLast rather than doFirst.

clean.doLast {
  ant.delete { fileset ( dir : '.' , includes : '**/*~, cobertura.ser, texput.log' ) }
  ant.delete ( dir :  'target_forMavenTest' )
}

//  The javadoc task in the java and groovy plugins is not up to anything as yet since the parameters appear
//  not to be setable.

/*
javadoc {
    title = "Gant API ${version}"
    bottom = 'Copyright &#169; 2006&#8211;9 Russel Winder.  All rights reserved.'
}
*/

createTask ( 'documentation' , dependsOn : 'compile' ) {
  def javadocDirPath = 'build/' + javadocDirName
  ant.delete ( dir : javadocDirPath , quiet : 'true' )
  ant.mkdir ( dir : javadocDirPath )
  def packageTitle = "Gant (${version})"
  def packageIncludeSpec = 'gant.*,org.codehaus.gant.*'
  def copyrightString = 'Copyright &#169; 1997&#8211;2009  The Codehaus.  All Rights Reserved.'
  def sourceDirectory = groovySrcDirs[0]
  ant.javadoc (
               sourcepath : sourceDirectory ,
               destdir : javadocDirPath ,
               packagenames :  packageIncludeSpec ,
               overview : 'overview.html' ,
               'private' : 'yes' ,
               encoding : 'UTF-8' ,
               use : 'true' ,
               author : 'true' ,
               version : 'true' ,
               windowtitle : packageTitle ,
               doctitle : packageTitle ,
               header : packageTitle ,
               footer : copyrightString ,
               source : sourceCompatibility
               ) {
    classpath {
      pathelement ( location : classesDir )
      //      path ( refid : classpathId )
    }
  }
  ant.groovydoc (
             sourcepath : sourceDirectory ,
             destdir : groovyApiDocumentationDirectory ,
             packagenames : packageIncludeSpec ,
             overview : 'overview.html' ,
             'private' : 'true' ,
             use : 'true' ,
             //author : 'true' ,
             //version : 'true' ,
             windowtitle : packageTitle ,
             doctitle : packageTitle ,
             header : packageTitle ,
             footer : copyrightString,
             //source : sourceCompatibility
             )
}

usePlugin ( 'maven' )
