//  Gant -- A Groovy build framework based on scripting Ant tasks.
//
//  Copyright Â© 2007-8 Russel Winder
//
//  Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in
//  compliance with the License. You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software distributed under the License is
//  distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
//  implied. See the License for the specific language governing permissions and limitations under the
//  License.
//
//  Author : Russel Winder <russel.winder@concertant.com>

final rootDirectory = '..' + System.properties.'file.separator' + '..'
final buildDirectory = rootDirectory + System.properties.'file.separator' + 'target'

ant.property ( file : rootDirectory + System.properties.'file.separator' + 'build.properties' )
ant.property ( file : rootDirectory + System.properties.'file.separator' + 'local.build.properties' )

gantVersion = ant.project.properties.gantVersion
groovyVersion = ant.project.properties.groovyVersion

final requiresGroovyPackageName = 'gant'
final standalonePackageName = 'gant-standalone'
final section = 'devel'
final priority = 'optional'
final architecture = 'all'
final homepage = 'http://gant.codehaus.org/' 

final installPrefixBase = 'usr/share/gant'

//  Use the ant-deb-task from Marius Scurtescu <marius.scurtescu@gmail.com> to create a deb file.
//  Assume the ant-deb.jar file is in ~/.groovy/lib.

ant.taskdef ( resource : 'ant_deb_task.properties' , classpath : [ System.properties.'user.home' , '.groovy' , 'lib' , 'ant-deb.jar' ].join ( System.properties.'file.separator' ) )

final debsDirectory = 'debs'

includeTargets << gant.targets.Clean
 cleanPattern << '**/*~'
cleanDirectory << debsDirectory

includeTool << gant.tools.Execute

theSynopsis = 'Gant -- Groovy scripting of Ant tasks.' 
theWords = """
Gant is a build tool for scripting Ant tasks using Groovy instead of XML
to specify the build logic. A Gant build specification is just a Groovy
script and so can bring all the power of Groovy to bear directly,
something not possible with Ant scripts. Whilst it might be seen as a
competitor to Ant, Gant relies on all the Ant  tasks for actually doing
things, so it is really an alternative way of doing builds using Ant,
but using a programming language rather than XML to specify the build
rules.
.
Homepage: ${homepage}
"""
  
target ( debFiles : 'Create the deb file of the distribution.' ) {
  execute.shell ( "cd ${rootDirectory} && gant debDistribution" )
  mkdir ( dir : debsDirectory )
  // Create a deb that depend on a Groovy deb                                                                                 
  deb ( todir : debsDirectory , 'package' : requiresGroovyPackageName , section : section , priority : priority , architecture : architecture ,
        depends : "java2-runtime, groovy ( >= ${groovyVersion} )", conflicts : standalonePackageName , postinst : 'postinst' , prerm : 'prerm' ) {
    version ( upstream : gantVersion )
    maintainer ( name : 'Russel Winder' , email : 'russel.winder@concertant.com' )
    //homepage ( homepage )
    description ( synopsis : theSynopsis , theWords )
    tarfileset ( file : [ rootDirectory , 'scripts' , 'bin_requiresGroovy' , 'gant' ].join ( System.properties.'file.separator' ) , prefix : installPrefixBase + '/bin' , filemode : '755' )
    tarfileset ( file : [ rootDirectory , 'scripts' , 'conf' , 'gant-starter.conf' ].join ( System.properties.'file.separator' ) , prefix : installPrefixBase + '/conf' )
    tarfileset ( file : buildDirectory + System.properties.'file.separator' + 'gant-' + gantVersion + '.jar' , prefix : installPrefixBase + '/lib' )
  }
  // Create a deb that is standalone requiring no Groovy installation. 
  deb ( todir : debsDirectory , 'package' : standalonePackageName , section : section , priority : priority , architecture : architecture ,
        depends : 'java2-runtime', conflicts : requiresGroovyPackageName , postinst : 'postinst' , prerm : 'prerm' ) {
    version ( upstream : gantVersion )
    maintainer ( name : 'Russel Winder' , email : 'russel.winder@concertant.com' )
    //homepage ( homepage )
    description ( synopsis : theSynopsis , theWords )
    tarfileset ( file : [ rootDirectory , 'target' , 'install' , 'bin' , 'gant' ].join ( System.properties.'file.separator' ) , prefix : installPrefixBase + '/bin' , filemode : '755' )
    tarfileset ( file : [ rootDirectory , 'target' , 'install' , 'bin' , 'startGroovy' ].join ( System.properties.'file.separator' ) , prefix : installPrefixBase + '/bin' )
    tarfileset ( file : [ rootDirectory , 'target' , 'install' , 'conf' , 'gant-starter.conf' ].join ( System.properties.'file.separator' ) , prefix : installPrefixBase + '/conf' )
    tarfileset ( file : buildDirectory + System.properties.'file.separator' + 'gant-' + gantVersion  + '.jar' , prefix : installPrefixBase + '/lib' )
    tarfileset ( dir : [ buildDirectory , 'install' , 'lib'].join (  System.properties.'file.separator' ) , includes : '*.jar' , prefix : installPrefixBase + '/lib' )
  }
}

setDefaultTarget ( debFiles )
