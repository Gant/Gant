//  Gant -- A Groovy way of scripting Ant tasks.
//
//  Copyright Â© 2007-8 Russel Winder
//
//  Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in
//  compliance with the License. You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software distributed under the License is
//  distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
//  implied. See the License for the specific language governing permissions and limitations under the
//  License.
//
//  Author : Russel Winder <russel.winder@concertant.com>

//  Sometimes we know the debDistribution target has been completed and so there is no need to do it again.

try { ignoreBuildDeb = Boolean.valueOf ( ignoreBuildDeb ) }
catch ( Exception e ) { ignoreBuildDeb = false }

//  If we are building the debs then we have probably just done a distribution and may want to not bother
//  with the tests.

try { skipTests = Boolean.valueOf ( skipTests ) }
catch ( MissingPropertyException mpe ) { skipTests = false }

final targetDirectoryName = 'target_gant'

final rootDirectory = '..' + System.properties.'file.separator' + '..'
final buildDirectory = rootDirectory + System.properties.'file.separator' + targetDirectoryName

ant.property ( file : rootDirectory + System.properties.'file.separator' + 'local.build.properties' )
ant.property ( file : rootDirectory + System.properties.'file.separator' + 'build.properties' )

final requiresGroovyPackageName = 'gant'
final standalonePackageName = 'gant-standalone'
final section = 'devel'
final priority = 'optional'
final architecture = 'all'
final homepage = 'http://gant.codehaus.org/' 

final installPrefixBase = 'usr/share/gant'

final antlibXMLns = 'antlib:org.apache.maven.artifact.ant'
final distributionURL = 'https://dav.codehaus.org/dist/gant/'

//  Use the ant-deb-task from Marius Scurtescu <marius.scurtescu@gmail.com> to create a deb file.
//  Assume the ant-deb.jar file is in ~/.groovy/lib.

ant.taskdef ( resource : 'ant_deb_task.properties' , classpath : [ System.properties.'user.home' , '.groovy' , 'lib' , 'ant-deb.jar' ].join ( System.properties.'file.separator' ) )

final debsDirectory = 'debs'
final scriptsDirectory = 'scripts'
final scriptsRequiresGroovyDirectory = scriptsDirectory + '/bin_requiresGroovy'
final scriptsStandaloneDirectory = scriptsDirectory + '/bin_standalone'

includeTargets << gant.targets.Clean
cleanPattern << '**/*~'
cleanDirectory << [ debsDirectory , scriptsDirectory ]

includeTool << gant.tools.Execute

theSynopsis = 'Gant -- Groovy scripting of Ant tasks.' 
theWords = """
Gant is a tool for scripting Ant tasks using Groovy instead of XML to specify
the logic. A Gant specification is just a Groovy script and so can bring all
the power of Groovy to bear directly, something not possible with Ant scripts.
Whilst it might be seen as a competitor to Ant, Gant relies on all the Ant 
tasks for the complex actions, so it is really an alternative way of doing
builds using Ant, but using a programming language rather than XML to specify
the rules.
.
Homepage: ${homepage}
"""
  
target ( debFiles : 'Create the deb file of the distribution.' ) {
  if ( ! ignoreBuildDeb ) { execute.shell ( "cd ${rootDirectory} && gant -DskipTests=${skipTests} debDistribution" ) }
  mkdir ( dir : scriptsRequiresGroovyDirectory )
  copy ( tofile : scriptsRequiresGroovyDirectory + '/gant' , file : 'gant.sh' ) {
    filterset { filter ( token : 'GANT_VERSION' , value : gantVersion ) }
    filterset { filter ( token : 'GROOVYJAR' , value : 'groovy-' + groovyDebianVersion + '.jar' ) }
  }
  mkdir ( dir : scriptsStandaloneDirectory )
  copy ( tofile : scriptsStandaloneDirectory + '/gant' , file : 'gant_standalone.sh' ) {
    filterset { filter ( token : 'GANT_VERSION' , value : gantVersion ) }
    filterset { filter ( token : 'GROOVYJAR' , value : 'groovy-all-' + groovyStandaloneVersion + '.jar' ) }
  }
  mkdir ( dir : debsDirectory )
  // Create a deb that depend on a Groovy deb                                                                                 
  deb ( todir : debsDirectory , 'package' : requiresGroovyPackageName , section : section , priority : priority , architecture : architecture ,
        depends : "java2-runtime, groovy ( >= ${groovyDebianVersion} ), ant ( >= ${ant.project.properties.antVersion} )",
        conflicts : standalonePackageName , postinst : 'postinst' , prerm : 'prerm' ) {
    version ( upstream : gantVersion )
    maintainer ( name : 'Russel Winder' , email : 'russel.winder@concertant.com' )
    //homepage ( homepage )
    description ( synopsis : theSynopsis , theWords.replace ( '''
.
''' , '''
.
This package depends on the groovy and ant packages.
.
''' ) )
    tarfileset ( file : scriptsRequiresGroovyDirectory + '/gant' , prefix : installPrefixBase + '/bin' , filemode : '755' )
    tarfileset ( file : [ rootDirectory , 'scripts' , 'conf' , 'gant-starter.conf' ].join ( System.properties.'file.separator' ) , prefix : installPrefixBase + '/conf' )
    tarfileset ( file : buildDirectory + System.properties.'file.separator' + 'gant-' + gantVersion + '.jar' , prefix : installPrefixBase + '/lib' )
  }
  // Create a deb that is standalone requiring no Groovy or Ant installation. 
  deb ( todir : debsDirectory , 'package' : standalonePackageName , section : section , priority : priority , architecture : architecture ,
        depends : 'java2-runtime',
        conflicts : requiresGroovyPackageName , postinst : 'postinst' , prerm : 'prerm' ) {
    version ( upstream : gantVersion )
    maintainer ( name : 'Russel Winder' , email : 'russel.winder@concertant.com' )
    //homepage ( homepage )
    description ( synopsis : theSynopsis , theWords.replace ( '''
.
''' , '''
.
This package provides a self-contained installation of Gant that does not depend
on a separate installation of Groovy -- all the jars needed for Gant to run are
included in the package.
.
''' ) )
    tarfileset ( file : scriptsStandaloneDirectory + '/gant' , prefix : installPrefixBase + '/bin' , filemode : '755' )
    tarfileset ( file : [ rootDirectory , targetDirectoryName , 'install' , 'bin' , 'startGroovy' ].join ( System.properties.'file.separator' ) , prefix : installPrefixBase + '/bin' )
    tarfileset ( file : [ rootDirectory , targetDirectoryName , 'install' , 'conf' , 'gant-starter.conf' ].join ( System.properties.'file.separator' ) , prefix : installPrefixBase + '/conf' )
    tarfileset ( dir : [ buildDirectory , 'install' , 'lib'].join (  System.properties.'file.separator' ) , includes : '*.jar' , prefix : installPrefixBase + '/lib' )
  }
}

target ( uploadDistribution : 'Upload the deb files to Codehaus.' ) {
  depends ( debFiles )
  "${antlibXMLns}:dependencies" ( pathId :  'slidePathId' ) {
    dependency ( groupId : 'slide' , artifactId : 'slide-webdavlib' , version : '2.1' )
  }
  def loader = getClass ( ).getClassLoader ( )
  def path = path ( refid : 'slidePathId' )
  ( path.list ( ) as List ).each { location -> loader.addURL ( new URL ( 'file://' + location ) ) }
  def settings =  ( new XmlSlurper ( ) ).parse ( System.properties.'user.home' + '/.m2/settings.xml' ).servers.server.find { item -> item.id == 'codehaus.org' }
  def credentials =  loader.loadClass ( 'org.apache.commons.httpclient.UsernamePasswordCredentials' ).getConstructor ( String , String ).newInstance ( settings.username.toString ( ) , settings.password.toString ( ) )
  //  Have to put resource in the binding so that the Closures can access it.
  resource = loader.loadClass ( 'org.apache.webdav.lib.WebdavResource' ).getConstructor ( String ,  loader.loadClass ( 'org.apache.commons.httpclient.Credentials' ) , boolean ).newInstance ( distributionURL , credentials , true )
  if ( resource.statusCode != 200 ) { println ( 'Failed to open ' + distributionURL ) }
  else {
    final serverUploadProducts = [:]
    final serverDistributionsDirectory = 'distributions'
    ( new File ( debsDirectory ) ).eachFile { file -> serverUploadProducts[ file.path ] = serverDistributionsDirectory + '/' + file.name
    }
    serverUploadProducts.each { source , destination ->
      def serverPath = resource.path + '/' + destination
      print ( 'Uploading ' + source + ' -> ' + destination + ' : ' )
      def result = 'Failed.'
      if ( resource.putMethod ( serverPath , new File ( source ) ) ) { result = 'OK.' }
      println ( result + ' Status : ' + resource.statusMessage )
    }
  }
  if ( resource != null ) { resource.close ( ) }
}

setDefaultTarget ( debFiles )
